module HideDelegate;

interface ClientI {
  Unit enquire(Bool rem);
}

interface PersonI {
  DeptI getDept();
  PersonI getManager(Int fromCog);
}

interface DeptI {
  PersonI getManager();
  Unit setManager(PersonI m);
}

interface CogMap {
  [Atomic] Unit add(Object o, Int cog);
  Maybe<Int> lookup(Object o);
  Bool hasCog(Int cog);
  Unit dump(); 
}

class CogMap implements CogMap {
  Map<Object,Int> cm = map[];
  Set<Int> cs = set[];
  
  [Atomic] Unit add(Object o, Int cog) {
    cm = put(cm,o,cog);
    cs = insertElement(cs,cog);
  }

  Maybe<Int> lookup(Object o) {
    return lookup(cm,o);
  }

  Bool hasCog(Int cog) {
   return contains(cs,cog);
  }

  Unit dump() {
    println("<Object,Cog> mapping: ");
    List<Pair<Object, Int>> es = entries(cm);
    foreach(e in es) {
      println(toString(e));
    }
  }

}


class Person(CogMap gm, Int cog, DeptI d) implements PersonI {
  {
    gm.add(this,cog);
  }

  PersonI getManager(Int fromCog) {
    println("Enter getManager");

    gm.dump();
    
    Maybe<Int> dbind = gm.lookup(d);

    switch(dbind) {
      Just(dcog) => {
          println("Enter assertion");
          assert dcog == cog || fromCog != dcog;
	}
      Nothing => {
          println("Should be unreachable");
          assert False;
	}
    }
    
    PersonI p = d.getManager();

    println("Leave getManager");
    return p;
  }

  DeptI getDept() {
    return d;
  }
}

class Dept(CogMap gm, Int cog) implements DeptI {
  PersonI m;
  {
    gm.add(this,cog);
  }
  
  PersonI getManager() {
    return m;
  }
  
  Unit setManager(PersonI p) {
    m = p;
  }

}

class Client(CogMap gm, Int cog) implements ClientI {
  {
    gm.add(this,cog);
  }
  
  Unit enquire(Bool remote) {
    DeptI d;
    Int dcog = when remote then cog+1 else cog;

    if (remote) {
      d = new Dept(gm,dcog);
    } else {
      d = new local Dept(gm,dcog);
    }

    PersonI m = new Person(gm, dcog+1,d);
    PersonI p = new Person(gm, dcog+2,d);
    d.setManager(m);

    /* Client -> Person -> Department */
    m = p.getManager(cog);
  }

}

{
  CogMap gm = new CogMap();
  ClientI c = new Client(gm,0);
  c.enquire(True);
}
